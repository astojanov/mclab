%import "lambda.ast.*";
%import "java.util.Collections";
%import "java.util.Iterator";
%import "java.util.List";

%package "lambda";

%class "LambdaParser";

%embed {:
    class Events extends Parser.Events {
        public void syntaxError(Symbol token) {
            StringBuffer s = new StringBuffer();
            s.append(token.getLine(token.getStart()) + ", " + token.getColumn(token.getStart()) + "\n");
            s.append("  *** Syntactic error: unexpected token " + Terminals.NAMES[token.getId()]);
            throw new RuntimeException(s.toString());
        }
        public void scannerError(Scanner.Exception e) {
            StringBuffer s = new StringBuffer();
            s.append(e.line + ", " + e.column + "\n");
            s.append("  *** Lexical error: " + e.getMessage());
            throw new RuntimeException(s.toString());
        }
    }

    { report = new Events(); } // Use error handler in parser
:};

%terminals IDENTIFIER, LAMBDA, ARG_BODY_SEP, LPAREN, RPAREN;
%typeof IDENTIFIER = "String";
%typeof Abs = "MultiAbs";
%typeof Var = "Var";
%typeof App, Base, Parens = "Node";

%goal Start;

Start
  = App.app {: return new Root(app); :}
  ;

App
  = Base.others+ Abs.abs? {:
        List<Node> nodes = new ArrayList<Node>(_list_others);
        if(abs != null) {
            nodes.add(abs);
        }
        Iterator<Node> it = nodes.iterator();
        Node curr = it.next();
        while(it.hasNext()) {
            curr = new App(curr, it.next());
        }
        return curr;
    :}
  | Abs //returns what the abs returns
  ;

Abs
  = LAMBDA Var.vars+ ARG_BODY_SEP App.body {:
        //NB: could easily de-sugar here (as in App, but this lets us test rewriting)
        lambda.ast.List<Var> allVars = new lambda.ast.List<Var>();
        for(Var var : vars) {
            allVars.add(var);
        } 
        return new MultiAbs(allVars, body);
    :}
  ;

Base
  = Parens //returns what the parens returns
  | Var    //returns what the var returns
  ;

Parens
  = LPAREN App.app RPAREN //returns what the nested app returns
  ;

Var
  = IDENTIFIER.var {: return new Var(var); :}
  ;