aspect StructureString {
    syn String ASTNode.getStructureString() {
        StringBuffer buf = new StringBuffer();
        for(beaver.Symbol comment : getComments()) {
            buf.append(comment.value);
            buf.append('\n');
        }
        buf.append(getStructureStringLessComments());
        return buf.toString();
    }

    //default - exception
    syn String ASTNode.getStructureStringLessComments() { throw new UnsupportedOperationException(getClass().getName() + ".getStructureStringLessComments()"); }
    
    //program
    eq Program.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        for(HelpComment comment : getHelpComments()) {
            buf.append(comment.getStructureStringLessComments());
            buf.append('\n');
        }
        for(Stmt stmt : getStmts()) {
            buf.append(stmt.getStructureString());
            buf.append('\n');
        }
        return buf.toString();
    }
    
    //help comments
    
    eq HelpComment.getStructureStringLessComments() = getText();
    
    //stmts
    eq EmptyStmt.getStructureStringLessComments() =  (isOutputSuppressed() ? ";" : "");
    eq ExprStmt.getStructureStringLessComments() = getExpr().getStructureStringLessComments() + (isOutputSuppressed() ? ";" : "");
    eq AssignStmt.getStructureStringLessComments() = getDest().getStructureStringLessComments() + " = " + getSource().getStructureStringLessComments() + (isOutputSuppressed() ? ";" : "");
    
    //TODO-AC: indentation for nested loops
    eq ForStmt.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append("for ");
        buf.append(getAssignStmt().getStructureStringLessComments());
        buf.append('\n');
        for(Stmt stmt : getStmts()) {
            buf.append('\t');
            buf.append(stmt.getStructureStringLessComments());
            buf.append('\n');
        }
        buf.append("end");
        return buf.toString();
    }
    
    //exprs
    eq ColonExpr.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append('(');
        buf.append(getBase().getStructureStringLessComments());
        buf.append(" : ");
        if(hasIncr()) {
            buf.append(getIncr().getStructureStringLessComments());
            buf.append(" : ");
        }
        buf.append(getLimit().getStructureStringLessComments());
        buf.append(')');
        return buf.toString();
    }
    eq Colon.getStructureStringLessComments() = ":";
    
    //accesses
    eq IdUse.getStructureStringLessComments() = getID();
    eq ParseName.getStructureStringLessComments() = getID();
    eq MethodAccess.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append(getID());
        buf.append('(');
        boolean first = true;
        for(Expr arg : getArgs()) {
            if(!first) {
                buf.append(", ");
            }
            buf.append(arg.getStructureStringLessComments());
            first = false;
        }
        buf.append(')');
        return buf.toString();
    }
    eq MatrixAccess.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append(getID());
        buf.append('(');
        boolean first = true;
        for(Expr arg : getArgs()) {
            if(!first) {
                buf.append(", ");
            }
            buf.append(arg.getStructureStringLessComments());
            first = false;
        }
        buf.append(')');
        return buf.toString();
    }
    
    eq MatrixDecl.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append('[');
        boolean first = true;
        for(MatrixRow row : getArgs()) {
            if(!first) {
                buf.append("; ");
            }
            buf.append(row.getStructureStringLessComments());
            first = false;
        }
        buf.append(']');
        return buf.toString();
    }
    eq MatrixRow.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        boolean first = true;
        for(Expr arg : getArgs()) {
            if(!first) {
                buf.append(", ");
            }
            buf.append(arg.getStructureStringLessComments());
            first = false;
        }
        return buf.toString();
    }
    
    //literals
    eq IntegerLiteral.getStructureStringLessComments() = getValue().getText();
    eq DoubleLiteral.getStructureStringLessComments() = getValue().getText();
    eq StringLiteral.getStructureStringLessComments() = "'" + getValue() + "'";
    
    eq MinusExpr.getStructureStringLessComments() = "(-" + getOperand().getStructureStringLessComments() + ")";
    eq PlusExpr.getStructureStringLessComments() = "(+" + getOperand().getStructureStringLessComments() + ")";
    eq LogNotExpr.getStructureStringLessComments() = "(~" + getOperand().getStructureStringLessComments() + ")";
    eq TransposeExpr.getStructureStringLessComments() = "(" + getOperand().getStructureStringLessComments() + "')";
    eq ArrayTransposeExpr.getStructureStringLessComments() = "(" + getOperand().getStructureStringLessComments() + ".')";
    
    syn String Binary.getStructureStringLessComments(String op) = "(" + getLeftOperand().getStructureStringLessComments() + " " + op + " " + getRightOperand().getStructureStringLessComments() + ")";
    
    eq MulExpr.getStructureStringLessComments() = getStructureStringLessComments("*");
    eq DivExpr.getStructureStringLessComments() = getStructureStringLessComments("/");
    eq LeftDivExpr.getStructureStringLessComments() = getStructureStringLessComments("\\");
    eq PowExpr.getStructureStringLessComments() = getStructureStringLessComments("^");
    eq AddExpr.getStructureStringLessComments() = getStructureStringLessComments("+");
    eq SubExpr.getStructureStringLessComments() = getStructureStringLessComments("-");
    eq EMulExpr.getStructureStringLessComments() = getStructureStringLessComments(".*");
    eq EDivExpr.getStructureStringLessComments() = getStructureStringLessComments("./");
    eq ELeftDivExpr.getStructureStringLessComments() = getStructureStringLessComments(".\\");
    eq EPowExpr.getStructureStringLessComments() = getStructureStringLessComments(".^");
    
    eq AndLogicalExpr.getStructureStringLessComments() = getStructureStringLessComments("&");
    eq OrLogicalExpr.getStructureStringLessComments() = getStructureStringLessComments("|");
    eq ShortCircuitAndExpr.getStructureStringLessComments() = getStructureStringLessComments("&&");
    eq ShortCircuitOrExpr.getStructureStringLessComments() = getStructureStringLessComments("||");
    eq LTExpr.getStructureStringLessComments() = getStructureStringLessComments("<");
    eq GTExpr.getStructureStringLessComments() = getStructureStringLessComments(">");
    eq LEExpr.getStructureStringLessComments() = getStructureStringLessComments("<=");
    eq GEExpr.getStructureStringLessComments() = getStructureStringLessComments(">=");
    eq EQExpr.getStructureStringLessComments() = getStructureStringLessComments("==");
    eq NEExpr.getStructureStringLessComments() = getStructureStringLessComments("~=");
}