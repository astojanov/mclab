
aspect AspectsStructureString {
      
    eq Aspect.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append("aspect ");
        buf.append(getName());
        buf.append('\n');
        for(HelpComment comment : getHelpComments()) {
            buf.append(comment.getStructureString());
            buf.append('\n');
        }
        for(Properties prop : getPropertys()) {
            buf.append(prop.getStructureString());
            buf.append('\n');
        }
        for(Methods methods : getMethods()) {
            buf.append(methods.getStructureString());
            buf.append('\n');
        }
        for(Patterns patterns : getPatterns()) {
            buf.append(patterns.getStructureString());
            buf.append('\n');
        }
        for(Actions actions : getActions()) {
            buf.append(actions.getStructureString());
            buf.append('\n');
        }
        buf.append("end");
        return buf.toString();
    }
    
    eq Patterns.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append("patterns ");
        buf.append('\n');
        for(Pattern pattern : getPatterns()) {
            buf.append(pattern.getStructureString());
            buf.append('\n');
        }
        buf.append("end");
        return buf.toString();
    }
    
    eq Pattern.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append(getName());
        buf.append(" : ");
        buf.append(getPD().getStructureString());
        buf.append(";");
        return buf.toString();
    }
    
    eq PatternDesignator.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append(getName());
        buf.append("(");
        boolean first = true;
        for(Name args : getArgs()) {
        	if(!first)
            	buf.append(", ");
            buf.append(args.getStructureString());
            first = false;
        }
        buf.append(")");
        return buf.toString();
    }
    
    eq Actions.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append("actions ");
        buf.append('\n');
        for(AspectAction aspectActions : getAspectActions()) {
            buf.append(aspectActions.getStructureString());
            buf.append('\n');
        }
        buf.append("end");
        return buf.toString();
    }
    
    eq AspectAction.getStructureStringLessComments() {
        StringBuffer buf = new StringBuffer();
        buf.append(getName());
        buf.append(" : ");
        buf.append(getType());
        buf.append(" ");
        buf.append(getPattern());
        boolean first = true;
        for(Name attr : getSelectors()) {
            if(first)
            	buf.append(" : (");            
            else
                buf.append(", ");
            buf.append(attr.getStructureString());
            first = false;
        }
        if(!first)
        	buf.append(")");
        buf.append('\n');
        for(HelpComment comment : getHelpComments()) {
            buf.append(comment.getStructureString());
            buf.append('\n');
        }
        for(Stmt stmt : getStmts()) {
            buf.append(stmt.getStructureString());
            buf.append('\n');
        }
        for(Function func : getNestedFunctions()) {
            buf.append(func.getStructureString());
            buf.append('\n');
        }
        buf.append("end");
        return buf.toString();
    }
}